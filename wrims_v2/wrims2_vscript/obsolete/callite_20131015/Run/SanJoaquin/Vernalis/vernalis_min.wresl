! vernalis_min.wresl

! Originally coded by Edward Chang (DWR) 06/27/2000 for Vernalis Minimum Flows

/*
 In Cycle 6:
 The following code defines and meets the 1995 Water Quality Control Plan codified in D1641
 (WQCP) Vernalis February thru June minimum flows (excluding the pulse flow period Apr15-May15).

 The Vernalis minimum flow is a function of:
     1)  60-20-20 index (San Joaquin basin water year type)
     2)  Where is 2ppt isohaline (measured as 2.64 mmhos/cm surface salinity) is required to be
 If the day weighted average requirement at Roe, Chipps and Confluence is less than
 or equal to 74 km, X2 is required to be at or West of Chipps, i.e. <= 74 km

                    X2 Required to be        X2 Required to be at or
                    East of Chipps           West of Chips
 Water year type    Minimum flow Req (cfs)   Minimum flow Req (cfs)
 ----------------------------------------------------------------
   Wet              2130                     3420
    AN              2130                     3420
    BN              1420                     2280
   Dry              1420                     2280
   Crt               710                     1140

 Release for the requirement is made from New Melones and may be restricted
 by a inputted limit on allocation of Stan water for this purpose.  See STAN_YR.TABLE
 The USBR 1997 two-year Interim Plan of Operations used the following allocation table:
 Water Supply   Annual
  Forecast     Allocation
   (TAF)         (TAF)
 -----------------------
     0.        0.
     1400.     0.
     2000.     0.
     2500.     1000.
     3000.     1000.
     6000.     1000.

where the Water Supply Forecast was comprised of End-of-Feb New Melones storage in taf + forecasted inflow
to New Melones from Mar1 to Sep30 in taf.  Usually in modeling thus far, this "forecast" was with perfect foresight.

Per NOAA BO Phase II Action IV.2.1, April 1 - May 31 flows at Vernalis based on WYT.
No longer providing these flows since BOR and SJRGA have no agreement on where they should come from -TF 090111
*/


/* RPA 60-day pulse flow (April-May).  Turn on or off  */
define NOAAVernMin_req {  
     case AprORMay {
          condition month == apr .or. month == may .and. Vern_RPA == 1 
          select NOAAmin from NOAAVern60dayMin where WYT = wyt_SJR }
     case otherwise {
          condition always
          value     0. }
          }
          
define NOAAVernMin_reqdv {alias NOAAVernMin_req kind 'flow-requirement' units 'cfs'}

define X2EastOfChipps {select VernMinReqEast from VernMin where wyt_SJR=wyt_SJR }
define X2WestOfChipps {select VernMinReqWest from VernMin where wyt_SJR=wyt_SJR }


/* Vernalis D-1641 baseflows (Feb-Apr 14, May 16-June) on and off  */
define X2VernMin_req {
     case considerX2 {
          condition month >= feb .and. month <= jun .and. D1641_Base == 1 
          value ((daysX2_chs*X2WestOfChipps)+((daysin - daysX2_chs)*X2EastOfChipps))/daysin}
     case otherwise {                                                                                                        !WB
          condition always                                                                                                   !WB
          value     0. }                                                                                                     !WB
          }                                                                                                                  !WB

define X2VernMin_reqdv {alias X2VernMin_req kind 'flow-requirement' units 'cfs'}

! the wqcp standard controls feb, mar, and jun
! the noaa req't controls in the pulse period of Apr/May
! the max of the noaa and wqcp standards controls in the non-pulse portion of Apr/May
define VernMin_req {
    case April {
        condition month==APR
        value max(NOAAVernMin_req, X2VernMin_req)*14./30. + NOAAVernMin_req*16./30.}
    case May {
        condition month==MAY
        value max(NOAAVernMin_req, X2VernMin_req)*16./31. + NOAAVernMin_req*15./31.}
    case otherwise {
        condition always
        value X2VernMin_req }
}

define VernMin_reqdv {alias VernMin_req kind 'flow-requirement' units 'cfs'}

/*
Vernalis minimum flow requirement deficiency. The deficiency is the
difference between the Vernalis minimum requirement less the San Joaquin River
inflow to the Delta in Cycle 2 (SJR_WQ1) and what is released from New Melones
for VAMP and/or DO in cycle 5 (VAMP_AND_DO).
*/
define VernMin_def1 {
    case April {
        condition month==APR
        value max(0., max(NOAAVernMin_req, X2VernMin_req) - C_SJRVER[SJR_WQ1])*14/30.   ! non-pulse
              + max(0., NOAAVernMin_req - C_SJRVER[VAMP_AND_DO])*16./30.}               ! pulse
    case May {
        condition month==MAY
        value max(0., max(NOAAVernMin_req, X2VernMin_req) - C_SJRVER[SJR_WQ1])*16/31.   ! non-pulse
              + max(0., NOAAVernMin_req - C_SJRVER[VAMP_AND_DO])*15./31.}               ! pulse
    case otherwise {
        condition always
        value max (0., VernMin_req - C_SJRVER[SJR_WQ1] - C_MELONDO[VAMP_AND_DO]) }
}

define VernMin_def_np {
    case AprilorMay {
        condition   range(month,apr,may)
        value       max(0., max(NOAAVernMin_req, X2VernMin_req) - C_SJRVER[SJR_WQ1])
    }
    case otherwise {
        condition   always
        value       max (0., VernMin_req - C_SJRVER[SJR_WQ1] - C_MELONDO[VAMP_AND_DO])
    }
}

define VernMin_def_p {
    case AprilorMay {
        condition   range(month,apr,may)
        value       max(0., NOAAVernMin_req - C_SJRVER[VAMP_AND_DO])
    }
    case otherwise {
        condition   always
        value       0.
    }
}

! Given the overall monthly deficiency now, deduct the release that New Melones
! is directed to make for the Stanislaus RPA flows, which will make its way to
! Vernalis.  This will give the actual final deficiency at Vernalis.
define VernMin_def {value max(0., VernMin_def1 - StanNMFSdef)}
define VernMin_defDV {alias VernMin_def kind 'flow-requirement' units 'cfs'}

/*
 Vernalis minimum flow requirement capapcity limits the amount of water to release
 based on the New Melones Forecast.
*/
define sumI_MELON_part_feb {
    case February{
        condition   month==FEB
        sum     (i=mar-month,mar-month+6,1) I_MELON(i) * cfs_TAF(i)
    }
    case otherwise {
        condition       always
        value       0. 
    }
}

define NMfore_feb_est {
    case February {
        condition   month == feb
        value       S_MELON[SJR_WQ1] + sumI_MELON_part_feb
    }
    case otherwise {
        condition   always
        value       0.
    }
}
define NMfore_feb_est_ {alias NMfore_feb_est kind 'forecast' units 'taf'}

define VernMinCap { 
    case February {
        condition   month == feb
        select      VernMinCap from stan_yr given NMF=NMfore_feb_est use minimum
    }
    case otherwise {
        condition   always
        select      VernMinCap from stan_yr given NMF=NMforecast1 use minimum
    }
}

! Vernalis minimum release cumulative total March through June (summed from Feb to current month-1).
define VernMinRelTotprv {
     case MarchthruJun {
          condition month >= mar .and. month <= jun
          sum(i=-1,prevfeb,-1) C_MELONmin(i)*cfs_taf(i) }
     case otherwise {
          condition always
          value     0. }
          }
/*
 Vernalis minimum release capacity remaining (Capacity less monthly cumulative total)
 February through June and reset each February.
*/
define VernMinCapRem {
     case February {
          condition month == feb
          value     VernMinCap*TAF_cfs }
     case MarchthruJun {
          condition month >= mar .and. month <= jun
          value     max (0.,VernMinCap - VernMinRelTotprv)*TAF_cfs }
     case otherwise {
          condition always
          value     0. }
          }
          
define VernMinCapRem_ {alias VernMinCapRem kind 'Remain-Release-limit' units 'cfs'}          
/*
 Vernalis minimum release remaining from Goodwin maximum capacity.  Pulse flow periods
 are addressed in April and May.  Flood flow does not count towards contributing
 water to the Vernalis minimum flow requirement.
*/
define VernMinRemGoodRipon1 {
     case April {
          condition month == apr
          value     max(0.,maxGoodwin - C_STANGDWN[SJR_WQ1] + C_STANGDWNF[SJR_WQ1])*14./30.
                  + max(0.,maxGoodwin - C_STANGDWN[VAMP_AND_DO] + C_STANGDWNF[VAMP_AND_DO])*16./30.}
     case MayOnly {
          condition month == may
          value     max(0.,maxGoodwin - C_STANGDWN[SJR_WQ1] + C_STANGDWNF[SJR_WQ1])*16./31.
                  + max(0.,maxGoodwin - C_STANGDWN[VAMP_AND_DO] + C_STANGDWNF[VAMP_AND_DO])*15./31.}
     case JunethruSep {
          condition month >= jun .and. month <= sep
          value max(0.,maxGoodwin - C_STANGDWN[VAMP_AND_DO] + C_STANGDWNF[VAMP_AND_DO]) }
     case otherwise {
          condition always
          value  max(0., maxGoodwin - C_STANGDWN[SJR_WQ1] + C_STANGDWNF[SJR_WQ1]) }
          }
define VernMinRemGoodRipon {value max(0., VernMinRemGoodRipon1 - StanNMFSdef)}

define VernMinRemGood_np {
    case AprilMay {
        condition   range(month,apr,may)
        value       max(0.,maxGoodwin - C_STANGDWN[SJR_WQ1] + C_STANGDWNF[SJR_WQ1])
    }
    case otherwise {
        condition   always
        value       max(0.,maxGoodwin - C_STANGDWN[VAMP_AND_DO] + C_STANGDWNF[VAMP_AND_DO])
    }
}
define VernMinRemGood_p {
    case AprilMay {
        condition   range(month,apr,may)
        value       max(0.,maxGoodwin - C_STANGDWN[VAMP_AND_DO] + C_STANGDWNF[VAMP_AND_DO])
    }
    case otherwise {
        condition   always
        value       0.
    }
}

define VernMinRel1_np {value min(VernMin_def_np,VernMinRemGood_np)}
define VernMinRel1_p {value min(VernMin_def_p,VernMinRemGood_p)}

define Rel1_np_frac {
    case April {
        condition   month == apr .and. VernMinRel1_np + VernMinRel1_p > 0.1
        value       VernMinRel1_np*cfs_taf*14./30./(VernMinRel1_np*cfs_taf*14./30. + VernMinRel1_p*cfs_taf*16./30.)
    }
    case May {
        condition   month == may .and. VernMinRel1_np + VernMinRel1_p > 0.1
        value       VernMinRel1_np*cfs_taf*16./31./(VernMinRel1_np*cfs_taf*16./31. + VernMinRel1_p*cfs_taf*15./31.)
    }
    case otherwise {
        condition   always
        value       1.
    }
}
    

define VernMinCaprem_np {
    case April {
        condition   month == apr
        value       Rel1_np_frac*VernMinCapRem*30./14.
    }
    case May {
        condition   month == may
        value       Rel1_np_frac*VernMinCapRem*31./16.
    }
    case otherwise {
        condition   always
        value       VernMinCapRem
    }
}

define VernMinCaprem_p {
    case April {
        condition   month == apr
        value       max(0.,1. - Rel1_np_frac)*VernMinCapRem*30./16.
    }
    case May {
        condition   month == may
        value       max(0.,1. - Rel1_np_frac)*VernMinCapRem*31./15.
    }
    case otherwise {
        condition   always
        value       0.
    }
}

define VernMinRel_np {value min(VernMinRel1_np,VernMinCaprem_np)}
define VernMinRel_p {value min(VernMinRel1_p,VernMinCaprem_p)}


/*
 Vernalis minimum flow requirement to be met is based on the least value of
 the maximum allowed release from Goodwin less what has been released for this month,
 or the Vernalis monthly flow requirement less flow in the Delta in cycle 1 (SJR_WQ1)
 less releases from New Melones in the DO cycle (VAMP_AND_DO) or the Vernalis
 minimum release capacity (limit) less the cumulative total released to meet the requirement.
*/
!define VernMin_reqtobemet { value min(VernMinRemGoodRipon, VernMin_def, VernMinCapRem)}
define VernMin_reqtobemet {
    case April {
        condition   month == apr
        value       (14.*VernMinRel_np + 16.*VernMinRel_p)/30.
    }
    case May {
        condition   month == may
        value       (16.*VernMinRel_np + 15.*VernMinRel_p)/31.
    }
    case otherwise {
        condition   always
        value       VernMinRel_np
    }
}

define AL_VrnMintobemet {alias VernMin_reqtobemet kind 'temp' units 'cfs'}


! Set minimum flow in C_MELON equal to the Vernalis minimum flow requirment to be met.
goal meetVernMin {
     lhs  C_MELONMIN
     case FebToJun {
          condition month >= feb .and. month <= jun
          rhs       VernMin_reqtobemet
          lhs<rhs   penalty 0. }
     case otherwise {
          condition always
          rhs       0. }
          }
