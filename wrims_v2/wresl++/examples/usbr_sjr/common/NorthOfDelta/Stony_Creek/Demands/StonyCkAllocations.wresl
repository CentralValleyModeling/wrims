! ProjectDemands.wresl
! R. Yaworski
! Date Unknown
!  Andy Draper 0Reclamation
! Revised1/31/2005 MWH


/* This file defines all demands (OUWUA, TCCA, etc.)
OUWUA South & North Canal demands based on 1999-2000 data.
*/

define StonyCkWSmaxdv  {std kind 'water-supply'  units 'taf'}
define StonyCkInflowdv {std kind 'flow-inflow'   units 'taf'}
define TCCA_demdv      {std kind 'demand-annual' units 'taf'}
define total_allocdv   {std kind 'demand-annual' units 'taf'}
define OUWUA_allocdv   {std kind 'demand-annual' units 'taf'}
define TCCA_allocdv    {std kind 'demand-annual' units 'taf'}


define apr_sep_inflow {
        case WY1922 { ! Special case for 1st water year
		condition month >= OCT .and. month <= MAR .and. wateryear == 1922
                select   apr_sep
                from     BlkBtForcastInflow
                where    wateryear = wateryear }
	case april_september {
		condition month == APR
		select 	apr_sep
		from 	BlkBtForcastInflow
		where wateryear = wateryear}
	case otherwise {
		condition always
		value StonyCkinflowdv(-1) }
		}

goal setStonyCkinflowdv {StonyCkinflowdv = apr_sep_inflow}	

define StonyCkWSmax {
	case WY1922 { ! Special case for 1st water year
		condition month >= OCT .and. month <= MAR .and. wateryear == 1922
		value 250.0 }
	case april_StonyCkWSmax {
		condition month == APR
		value S_EPARK(prevMAR) + S_SGRGE(prevMAR) + S_BLKBT(prevMAR) + apr_sep_inflow}
	case otherwise {
		condition always
		value StonyCkWSmaxdv(-1) }
		}

goal setStonyCkWSmaxdv {StonyCkWSmaxdv = StonyCkWSmax}

define carryover_target {
	case carryover_trigger {
		condition StonyCkWSmax <= 60.0
		value 5.0 }
	case otherwise {
		condition always
		value 32.5 }
		}

define evap_tot {
	case evap_trigger {
		condition StonyCkWSmax <= 60.0
		value 5.0 }
	case otherwise {
		condition always
		value 12.5 }
		}

define StonyCkSupply   {value StonyCkWSmax - carryover_target - evap_tot}
define StonyCkSupplydv {alias StonyCkSupply kind 'supply' units 'taf'}


! Define annual applied water demand
define AnnAW_6_PAdv {std kind 'demand' units 'taf'}
define AnnAW_6_PA {

       case Oct1922 {  ! special case for first wateryear, Sum Oct-Sep
       condition month == OCT .and. wateryear == 1922
       sum(i=0,11,1) AW_6_PA(i)*cfs_taf(i) }

       case Mar2006 {  ! special case for last wateryear, Sum Oct-Sep
       condition month == MAR .and. wateryear == 2006
       sum(i=prevOCT,SEP-month,1) AW_6_PA(i)*cfs_taf(i) }

       case April { ! April for wateryears 1922 - 2002
       condition month == APR .and. wateryear < 2006
       sum(i=0,11,1) AW_6_PA(i)*cfs_taf(i) }

       case Rest {
       condition always
       value AnnAW_6_PAdv(-1) }
}

goal set_AnnAW_6_PA {AnnAW_6_PAdv = AnnAW_6_PA}

define AnnGPmin_6_PAdv {std kind 'min-pump' units 'taf'}
define AnnGPmin_6_PA {

       case Oct1922 {  ! special case for first wateryear, Sum Oct-Sep
       condition month == OCT .and. wateryear == 1922
       sum(i=0,11,1) GPmin_6_PA*AW_6_PA*cfs_taf(i) }

       case Mar2006 {  ! special case for last wateryear, Sum Oct-Sep
       condition month == MAR .and. wateryear == 2006
       sum(i=prevOCT,SEP-month,1) GPmin_6_PA*AW_6_PA*cfs_taf(i) }

       case April { ! April for wateryears 1922 - 2002
       condition month == APR .and. wateryear < 2006
       sum(i=0,11,1) GPmin_6_PA*AW_6_PA*cfs_taf(i) }

       case Rest {
       condition always
       value AnnGPmin_6_PAdv(-1) }
}

goal set_AnnGPmin_6_PA {AnnGPmin_6_PAdv = AnnGPmin_6_PA}


define AnnCL_6_PAdv {std kind 'contract-limit' units 'taf'}
define AnnCL_6_PA {value CL_STN026_6_PA + CL_STN021_6_PA }

/*       case Oct1922 {  ! special case for first wateryear, Sum Oct-Sep
       condition month == OCT .and. wateryear == 1922
       sum(i=0,11,1) (CL_STN026_6_PA(i)+CL_STN021_6_PA(i)) }

       case Mar2006 {  ! special case for last wateryear, Sum Oct-Sep
       condition month == MAR .and. wateryear == 2006
       sum(i=prevOCT,SEP-month,1)  (CL_STN026_6_PA(i)+CL_STN021_6_PA(i)) }

       case April { ! April for wateryears 1922 - 2002
       condition month == APR .and. wateryear < 2006
       sum(i=0,11,1)  (CL_STN026_6_PA(i)+CL_STN021_6_PA(i)) }

       case Rest {
       condition always
       value AnnCL_6_PAdv(-1) }
*/

goal set_AnnCL_6_PA {AnnCL_6_PAdv = AnnCL_6_PA}

define OUWUA_maxdr   {value max(0.0,min((AnnAW_6_PA-AnnGPmin_6_PA)  * (1+ RPF_6_PA)  * (1+ RUF_6_PA)  *(1 + EVF_STN_6_PA  + DPF_STN_6_PA + LFF_STN_6_PA + SPF_STN_6_PA),AnnCL_6_PA))}
define OUWUA_maxdrdv {alias OUWUA_maxdr kind 'demand' units 'taf'}

define TCCA_dem {value 38.3}

goal setTCCA_demdv {TCCA_demdv = TCCA_dem}

define StonyCkAlloc { value max(0.0,min(OUWUA_maxdr + TCCA_dem,StonyCkSupply)) }
define StonyCkAllocdv {std kind 'allocation' units 'taf'}

goal setStonyCkAllocdv {StonyCkAllocdv = StonyCkAlloc}

define alloc_OUWUA {
	case met_OUWUA {
		condition StonyCkAlloc >= OUWUA_maxdr
		value OUWUA_maxdr }
	case otherwise {
		condition always
		value StonyCkAlloc }
		}

goal setOUWUA_allocdv {OUWUA_allocdv = alloc_OUWUA}

define alloc_TCCA {
	case met_TCCA {
		condition StonyCkAlloc >= OUWUA_maxdr + TCCA_dem
		value TCCA_dem }
	case partial_TCCA {
		condition StonyCkAlloc >= OUWUA_maxdr
		value StonyCkAlloc - OUWUA_maxdr }
	case otherwise {
		condition always
		value 0.0 }
		}

goal setTCCA_allocdv {TCCA_allocdv = alloc_TCCA}


! define the monthly allocation for OUWUA South Canal (units = cfs)
define alloc_OUWUA_s     {VALUE max(0.0,min(1.0,(alloc_OUWUA/OUWUA_maxdr))*0.68*(AW_6_PA-GPmin_6_PA) *(1+ RPF_6_PA)  * (1+ RUF_6_PA)  *(1 + EVF_STN_6_PA  + DPF_STN_6_PA + LFF_STN_6_PA + SPF_STN_6_PA)) }
goal set_D_STN026_6_PA   { D_STN026_6_PA  < alloc_OUWUA_s}

! define the monthly allocation for OUWUA North Canal (units = cfs)
define alloc_OUWUA_n     {VALUE max(0.0,min(1.0,(alloc_OUWUA/OUWUA_maxdr))*0.32*(AW_6_PA-GPmin_6_PA) *(1+ RPF_6_PA)  * (1+ RUF_6_PA)  *(1 + EVF_STN_6_PA  + DPF_STN_6_PA + LFF_STN_6_PA + SPF_STN_6_PA)) }
goal set_D_STN021_6_PA   { D_STN021_6_PA  < alloc_OUWUA_n}

! define monthly distribution percent for TCCA
define per_TCCA_tot      {SELECT per_TCCA FROM TCCA_per where month=month }
goal set_D_STN014_TCC031 { D_STN014_TCC031 < alloc_TCCA*per_TCCA_tot*taf_cfs}
