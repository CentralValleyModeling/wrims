! R.Wilbur & S. Wu 5/10/05
/*

    ARTIFICIAL NEURAL NETWORK
    DELTA SALINITY MODELING

    ANN determines salinity (micro-mhos/cm) at various locations in the Delta given flows
    (Sac, SWP pumping, CVP pumping, SJR, Channel depletions) and the Delta cross-channel
    position for the past 118 days

    In this CALSIM modeling implementation the 78 days are split into three parts: previous two months
    and current month.  The previous months flows and gate positions are known values, while the
    current month flows (at least SAC and Exports) are unknown variables. These
    are the 2 knobs used to meet salinity standards in the Delta.

    this part of WRESL code defines some variables used for X2 estimation with ANN.

   AVE_TYPE KEY:
      1 = monthly average
      2 = first day of month value
      3 = last day of month value
      4 = maximum daily value
      5 = minimum daily vlaue
      6 = maximum 14-day value
      7 = average for first 15 days
      8 = average for last 15 days

*/

define monthly_ave {value 1}
define first_day {value 2}
define last_day {value 3}
define max_day {value 4}
define min_day {value 5}
define max_14day_running_ave {value 6}
define ave_first_15days {value 7}
define ave_last_15days {value 8}

define daysindv {alias daysin kind 'DAYS' units 'DAYS'}

!*************************************************************************************************************
! define the other flows that go into making the export component
define net_delta_cu { value                       ! Net Delta Consumptive Use
!        DEMAND_D400B - I400 +    ! Don't include this because it occurs upstream of C400
        DEMAND_D404 - I404 +
        DEMAND_D406 - I406B +
        DEMAND_D413 - I413 +
        DEMAND_D412 - I412 +
        DEMAND_D410 - I410 +
        DEMAND_D409B - I409
}

define net_DICU{ lower unbounded kind 'DICU_FLOW' units 'CFS' }!shengjun add 2/24/2005
goal def_net_DICU{ net_DICU=
!        D400B - I400 +           ! Don't include this because it occurs upstream of C400
        D404 - I404 +
        D406 - I406B +
        D413 - I413 +
        D412 - I412 +
        D410 - I410 +
        D409B - I409
}

! Net Delta Consumptive Use decision variable for calculating ANN MRDO
define net_delta_cuDV { ALIAS net_DICU KIND 'NET-DICU' UNITS 'CFS' }

!*************************************************************************************************************
! other defines
define DXC {lower unbounded kind 'GATE-DAYS-OPEN' units 'NONE'}
define sac_oth { std kind 'FLOW-INFLOW' units 'CFS' }
define exp_oth { lower unbounded kind 'FLOW-EXPORTS' units 'CFS' }
define VernWQfinal {std kind 'Salinity-EC' units 'umhos/cm'}
define MRDO_X2nocapdv {std KIND 'FLOW-REQ-MRDO' UNITS 'CFS'}

!*************************************************************************************************************
