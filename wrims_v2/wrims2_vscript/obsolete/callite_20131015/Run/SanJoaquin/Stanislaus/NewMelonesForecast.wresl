! NewMelonesForecast.wresl
! Edward Chang
! 4/04/00
! This code calculates two separate New Melones Forecasts
! Revised by C. Hsu, 2/26/03, USBR, for review and documentation project
!######################################################################################

/*
The New Melones forecasts are used to determine demands and minimum flows on the Stanislaus River.
The first forecast (NMforecast1) is the February end of month storage + March thru September
inflows.  This forecast is updated each March.  NMforecast1 is used to determine the minimum fish
and wildlife flows, the Stockton East demands, and the Central San Joaquin demands.  The second
forecast (NMforecast2) is the sum of October thru September inflows.  This forecast is updated
each October.  NMforecast2 is used to determine the Oakdale Irrigation District and the Southern
San Joaquin Irrigation District.
*/
! Define New Melones inflows ( sum of March through September inflows)
define sumI_MELON_part {
    case start{
        condition   month<=FEB .and. wateryear==beginWY
    value 1000.}
    case marthrusep {
        condition   month >= mar .and. month <= sep
        sum(i=mar-month,mar-month+6,1) I_MELON(i) * cfs_TAF(i) }
    case otherwise {
        condition   always
        sum(i=-month-mar,-month,1) I_MELON(i) * cfs_TAF(i) }
        }
! New Melones forecast 1, is the February end of month storage plus March through September inflows
define NMforecast1 {
     case FebruaryBeginWY {
          condition wateryear==beginWY .and. month <= feb
          value        1700. + /*  S_MELON(-12) + */ sumI_MELON_part }
     case February {
          condition month == feb
          value          S_MELON(-12) + sumI_MELON_part }
    case marchtofeb {
        condition   always
        value       S_MELON(prevfeb) + sumI_MELON_part }
        }
! New Melones forecast 2, is the sum of October thru September inflows
define NMforecast2 {
     case October {
          condition month == oct
          sum(i=0,11,1) I_MELON(i) * cfs_TAF(i) }
     case otherwise {
          condition always
          sum(i=prevoct,sep-month,1) I_MELON(i) * cfs_TAF(i) }
          }

/*
Define annual delivery quantity using look up table.  Starts at march and forecasts through feb
to be used with stanislaus minflows in cycle 1 and 3
*/
!define stanfish_yr { select stanf from stan_yr given NMF = NMforecast1 use linear }
! Alias stanfih_yr for output
!define stanfish_yrDV {alias stanfish_yr units 'taf'}

define sumI_MELONp_a     {alias sumI_MELON_part kind 'inflow' units 'taf'}
define NMforecast1dv {alias NMforecast1 kind 'forecast' units 'taf'}
define NMforecast1_a {alias NMforecast1 kind 'forecast' units 'taf'}
define NMforecast2_a {alias NMforecast2 kind 'forecast' units 'taf'}
