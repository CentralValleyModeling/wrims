/**************************************
NegCarriageOpsLimit.wresl

Nazrul Islam; DWR; (06/27/2010)
************************************/
define est_exp_2 {value max(ExportCap,EstTotExpTD)}
define exp_est_total_2 {alias est_exp_2 kind 'ESTIMATE' units 'CFS'}

!Emmaton
define est_req_sac_em {
  case NegCarriage{
    condition EM_m_sv >= 1.0
    value (est_exp_2-EM_b_sv)/EM_m_sv}
  case Normal {
    condition always
    value 0.}
}
define req_sac_est_em {alias est_req_sac_em kind 'ESTIMATE' units 'CFS'}

!Rock Slough
define est_req_sac_rs {
  case NegCarriage{
    condition RS_m_1_sv >= 1.0
    value (est_exp_2-RS_b_1_sv)/RS_m_1_sv}
  case Normal {
    condition always
    value 0.}
}
define req_sac_est_rs {alias est_req_sac_RS kind 'ESTIMATE' units 'CFS'}

!Antioch
if int(AN_DLTSW)==3 {
	define est_req_sac_an {
	  case NegCarriage{
	    condition an_m_sv >= 1.0
	    value (est_exp_2-an_b_sv)/an_m_sv}
	  case Normal {
	    condition always
	    value 0.}
	}
	define req_sac_est_an {alias est_req_sac_an kind 'ESTIMATE' units 'CFS'}
}


!Chipps
if int(CH_DLTSW)==3 {
	define est_req_sac_ch {
	  case NegCarriage{
	    condition ch_m_sv >= 1.0
	    value (est_exp_2-ch_b_sv)/ch_m_sv}
	  case Normal {
	    condition always
	    value 0.}
	}
	define req_sac_est_ch {alias est_req_sac_ch kind 'ESTIMATE' units 'CFS'}

	goal target_sac{
	  lhs C_Hood_ANN 
	!/* Don't use this goal if IF is available as alternate export pathway
	  case NegCarriageBoth{
		condition EM_m_sv >= 1.0 .or. RS_m_1_sv >= 1.0 .or. AN_m_sv >= 1.0 .or. CH_m_sv >= 1.0
		rhs max(est_req_sac_em,est_req_sac_rs,est_req_sac_an,est_req_sac_ch)
		lhs>rhs penalty 1000
		lhs<rhs penalty 0}
	!*/
	  case Normal {
		condition always
		rhs 0.
		lhs>rhs penalty 0
		lhs<rhs penalty 0}
	}
}

if int(CH_DLTSW) == 0 .or. int(AN_DLTSW) == 0 {
	goal target_sac{
	  lhs C_Hood_ANN 
	!/* Don't use this goal if IF is available as alternate export pathway
	  case NegCarriageBoth{
		condition EM_m_sv >= 1.0 .or. RS_m_1_sv >= 1.0 
		rhs max(est_req_sac_em,est_req_sac_rs)
		lhs>rhs penalty 1000
		lhs<rhs penalty 0}
	!*/
	  case Normal {
		condition always
		rhs 0.
		lhs>rhs penalty 0
		lhs<rhs penalty 0}
	}
}