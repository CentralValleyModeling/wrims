! bounds_cycle4.wresl
! Edward Chang
! 5/1/2000
! Joint Model
! This file bounds decision variables within the San Joaquin basin for cycle 4 only
! Revised by C. Hsu, 3/5/03, USBR, for review and documentation project
!####################################################################################
/*
This code bounds decision variables for cycle 4
*/
! Restrict DMC inflows to the Mendota Pool
goal restrC708cycle4 { C708 < C708[SJR_WQ1] }
! Mendota Pool Exchange donation to VAMP
goal restrC607BVAMPcycle4 { C607BVAMP = 0. }

/*
Add-water releases
*/
! New Melones dissolved oxygen release
goal setC10DOcycle4 { C10DO = 0. }             
! New Melones mainstem VAMP release
goal setC10vamp_mcycle4 { C10vamp_m = 0. }  
! New Melones VAMP transfer release
goal setC10vamp_tcycle4 { C10vamp_t = 0. }  
! Don Pedro VAMP release
goal setC81vampcycle4 { C81vamp = 0. }     
! Don Pedro VAMP release
goal setC81stanvampcycle4 { C81_stan_vamp = 0. }  
! McClure Vamp release
goal setC20vampcycle4 { C20vamp = 0. }      
! February Thru June 1995 WQCP Vernalis Minflow release
goal setC10MINcycle4 { C10MIN = 0. }         
! October Merced Releases
goal setC20MINcycle4 { C20MIN = 0. }         
! New Melones RPA release
goal setC10RPAcycle4 { C10RPA = 0. }    
! OID reallocated water
goal setC10INSTREAMcycle4 { C10INSTREAM = C10INSTREAM[sjrbase] }    
! Debt is incurred in cycle 5 but payment is not made until cycle 6 
goal setD530_VAMP {D530_VAMP = 0.}          

!Tuolumne River FERC minimum flows
goal boundC540cycle4 { 
    lhs C540_mif 
    case AprMay {
        condition   month >= apr .and. month <= may
        rhs         C540_mif[SJR_PULSE]
        lhs<rhs     penalty 0 }
    case otherwise {
        condition   always
        rhs         C540_mif[SJR_WQ1]
        lhs<rhs     penalty 0 }
        }

! Stanislaus River below Goodwin (Reach C520) F & W minimum flows 
goal boundC520cycle4 { 
    lhs C520_mif 
    case AprMay {
        condition   month >= apr .and. month <= may
        rhs         C520_mif[SJR_PULSE]
        lhs<rhs     penalty 0 }
    case otherwise {
        condition   always
        rhs         C520_mif[SJR_WQ1]
        lhs<rhs     penalty 0 }
        }
! Stanislaus River below Ripon (Reach C528) F & W minimum flows  
goal boundC528cycle4 { 
    lhs C528_mif 
    case AprMay {
        condition   month >= apr .and. month <= may
        rhs         C528_mif[SJR_PULSE]
        lhs<rhs     penalty 0 }
    case otherwise {
        condition   always
        rhs         C528_mif[SJR_WQ1]
        lhs<rhs     penalty 0 }
        }
! Stanislaus River below Goodwin excess flows 
goal bound1C520_exc_acycle4 { 
     lhs       C520_exc_a
    case AprMay {
        condition   month >= apr .and. month <= may
            rhs       C520_exc_a[SJR_PULSE] 
            lhs<rhs   penalty 0 }
          }
! Stanislaus River below Ripon excess flows  
goal bound2C520_exc_acycle4 { 
     lhs       C520_exc_a
    case AprMay {
        condition   month >= apr .and. month <= may
            rhs       C520_exc_a[SJR_PULSE] 
            lhs>rhs   penalty 0 }
          }


/*
VAMP book-keeping
*/
! VAMP April release from Merced ID 
goal setMercedIDaddAprc4 { MercedIDaddApr =  0. }
! VAMP April release from OID
goal setOIDaddAprc4 { OIDaddApr = 0. }
! VAMP April release from Exchange Contractors 
goal setExchangeaddAprc4 { ExchangeaddApr = 0. }
! VAMP April release from MID/TID
goal setMIDTIDaddAprc4 { MIDTIDaddApr = 0. }
