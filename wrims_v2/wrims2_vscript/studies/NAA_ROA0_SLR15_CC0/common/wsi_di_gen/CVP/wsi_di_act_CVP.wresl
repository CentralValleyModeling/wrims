/*This file generates the values that will be used in the WSI-DI curve generation
  Dustin Jones 01/07/99
*/

!Sanjay Pahuja/Rob Leaf  HILL 1/9/02
!di modified to include trinity
! Define variables to be used in WSI-DI curve generation
define DI_act_CVP_s    {std kind 'DEMAND-INDEX' units 'TAF'}
define DI_act_CVP_sys  {std kind 'DEMAND-INDEX' units 'TAF'}
define WSI_act_CVP_s   {std kind 'WATER-SUPPLY-INDEX' units 'TAF'}
define WSI_act_CVP_sys {std kind 'WATER-SUPPLY-INDEX' units 'TAF'}

!  =============================== Set DI values ===================================
!  The demand index for the southern portion of the system is comprised of all southern
!  deliveries and carryover storage for Trinity, Shasta, Folsom, and CVP San Luis.

define S4_ERPprev    {value min(max(ERPStorReserve-S32(prevSEP),0.),S4(prevSEP) )}       !has value if S32 running lower than ERPStorReserve guidance
define S32_ERPprev   {value min(    ERPStorReserve                 ,S32(prevSEP))}       !has value according to S32 within ERPStorReserve guidance
define S32_otherprev {value min(max(S32(prevSEP)-S32_ERPprev-CVPStorReserve,0.),S32(prevSEP)-S32_ERPprev)}  !has value if remaining storage exists above ERPStorReserve and CVPStorReserve guidance

goal set_DI_CVP_s {
     lhs  DI_act_CVP_s
     case MAR {
          condition      month == MAR
          rhs            tot_del_cvp_s + S1(prevSEP) +
                         S4(prevSEP) - S4_ERPprev + S44(prevSEP) +
                         S8(prevSEP) + S11(prevSEP) +
                         S32(prevSEP) - S32_ERPprev - S32_otherprev}

     case other {
          condition      always
          rhs            0.0 }
}

! The demand index for the entire system is comprised of all northern + southern
! deliveries and carryover storage for Trinity, Shasta, Folsom, and CVP San Luis.
goal set_DI_CVP_sys {
     lhs  DI_act_CVP_sys
     case MAR {
          condition      month == MAR
          rhs            tot_del_cvp_n + tot_del_cvp_s + S1(prevSEP) +
                         S4(prevSEP) - S4_ERPprev + S44(prevSEP) +
                         S8(prevSEP) + S11(prevSEP) +
                         S32(prevSEP) - S32_ERPprev - S32_otherprev}

     case other {
          condition      always
          rhs            0.0 }
}

! =============================== Set WSI values ===================================
! The WSI values for the south and entire system are defined in the CVP_dellogic files.
! We use March and April because of the conservative forecasts, 90% and 75% respectively.
! May has a 50% exceedence level forecast so we don't use it for the curve generation.

goal set_WSI_CVP_sys {
     lhs  WSI_act_CVP_sys
     case APR_MAY {
          condition      month == APR .or. month == MAY
          rhs            WSI_CVP_sys }
     case other {
          condition      always
          rhs            0.0 }
}

!define erik_n {alias tot_del_cvp_n kind 'test' units 'taf'}
!define erik_s {alias tot_del_cvp_s kind 'test' units 'taf'}
!define erik_sdem {alias tot_dem_cvp_s kind 'test' units 'taf'}
