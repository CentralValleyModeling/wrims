! E.Reyes 6/26/98
! A. Munevar 11/13/99

! Defines Standard allowable & Max allowable pumping at Banks.  Banks can increase pumping
! up to 1/3 of San Joaquin River flow at Vernalis if flow at Vernalis is greater than 1000 cfs.

!   If SJR flow @ Vernalis > 1000 cfs then,
!   Banks_max_allow = Banks_standard_allow + 1/3*(SJR @ Vernalis)
!   But only upto physical capacity.

! Number of days in which the Banks limit *IS* increased by 1/3 of SJR flow
define BanksDaysIncr {
       select   DaysIncr
       from    BanksLimits
       where   month=month
}

! Number of days in which the Banks limit *IS NOT* increased by 1/3 of SJR flow
define BanksDaysNoIncr { value daysin - BanksDaysIncr}

! Finds the Max Pump Capacity with the allowable increase
define BanksMax1 {
    case Increase {
         condition  C_SJR070[VAMP_AND_DO] > 1000.0
         value      min(BanksCapPermit + C_SJR070[VAMP_AND_DO]/3.0, BanksCapPhys) }
    case NoIncrease {
         condition  always
         value      BanksCapPermit }
}

! Limit Pumping to allowable, weight for days in which increase is allowed
define BanksMaxAllow {value BanksMax1*BanksDaysIncr/daysin + BanksCapPermit*BanksDaysNoIncr/daysin }

! Limit Pumping to Allowable and Physical Capacity, whichever is less
goal banksLessThanAllow { CAA000_SWP + CAA000_CVP < BanksMaxAllow}
goal banksLessThanPhys  { C_CAA000 < BanksCapPhys}

! Output Allowable Pumping Cap for Checking Only
define BanksAllowOut {alias BanksMaxAllow kind 'FLOW-ALLOW-BANKS' UNITS 'CFS' }

! Banks PP Minimum Cap
goal setbanksminpump {
    lhs CAA000_SWP
    rhs 300.
    lhs>rhs penalty 0
    lhs<rhs penalty 2700
}

