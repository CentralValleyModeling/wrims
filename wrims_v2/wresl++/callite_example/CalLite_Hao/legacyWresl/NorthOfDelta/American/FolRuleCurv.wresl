define OctMarRunoffEst {select OctMar from SacValleyIndex where wateryear=wateryear}

define S_FolsmLevel3 {
    case OctthruDec {
             condition month >= Oct .and. month <= Mar .and. OctMarRunoffEst > 14.33 !25% Exceedence level for Oct-Mar 40-30-30 Runoff
        value 300.0 }
    case Other {
        condition always
        value 400.0 }
}

define FolInf {value (I_Folsm + R_Nimbus + I300)*cfs_taf }
define FolInfEst {sum(i=0,SEP-month,1) (I_Folsm(i)+R_Nimbus(i)+I300(i))*cfs_taf(i) }

define FolDiv {value (DEM_D300_WR_ANN + DEM_D300_PMI_ANN) * perdem_70pcwa +
                      DEM_D8A_WR_ANN * perdem_70nrwd +
                     (DEM_D8B_WR_ANN + DEM_D8B_PMI_ANN) * perdem_70fol +
                      DEM_D8C_WR_ANN * perdem_70folp +
                      DEM_D8D_WR_ANN * perdem_70sjwdp +
                     (DEM_D8E_WR_ANN + DEM_D8E_PMI_ANN) * perdem_70sjwds +
                     (DEM_D8F_WR_ANN + DEM_D8F_PMI_ANN) * perdem_70eldor +
                     (DEM_D8G_WR_ANN + DEM_D8G_PMI_ANN) * perdem_70rose +
                      DEM_D8h_PMI_ANN * perdem_70pcwa +
                      DEM_D8i_PMI_ANN * perdem_70eldorco +
                      DEM_D9AA_WR_ANN * perdem_70scwc +
                      DEM_D9AB_PMI_ANN * perdem_70carec +
                      DEM_D9A_PLS * cfs_taf +
                      DEM_D9B_PMI_ANN * perdem_70smud}

define FolDivEst {sum(i=0,SEP-month,1)
            ((DEM_D300_WR_ANN(i) + DEM_D300_PMI_ANN(i)) * perdem_70pcwa(i) +
                      DEM_D8A_WR_ANN(i) * perdem_70nrwd(i) +
                     (DEM_D8B_WR_ANN(i) + DEM_D8B_PMI_ANN(i)) * perdem_70fol(i) +
                      DEM_D8C_WR_ANN(i) * perdem_70folp(i) +
                      DEM_D8D_WR_ANN(i) * perdem_70sjwdp(i) +
                     (DEM_D8E_WR_ANN(i) + DEM_D8E_PMI_ANN(i)) * perdem_70sjwds(i) +
                     (DEM_D8F_WR_ANN(i) + DEM_D8F_PMI_ANN(i)) * perdem_70eldor(i) +
                     (DEM_D8G_WR_ANN(i) + DEM_D8G_PMI_ANN(i)) * perdem_70rose(i) +
                      DEM_D8h_PMI_ANN(i) * perdem_70pcwa(i) +
                      DEM_D8i_PMI_ANN(i) * perdem_70eldorco(i) +
                      DEM_D9AA_WR_ANN(i) * perdem_70scwc(i) +
                      DEM_D9AB_PMI_ANN(i) * perdem_70carec(i) +
                      DEM_D9A_PLS(i) * cfs_taf(i) +
                      DEM_D9B_PMI_ANN(i) * perdem_70smud(i)) }

define FolSupEst {value max(0.0,FolInfEst - FolDivEst + S_Folsm(-1) - S_FolsmLevel3)}

define FolSupEstdv {alias FolSupEst kind 'FORECAST' units 'TAF'}

define FolFlowTarg {value (FolSupEst/cfs_taf)/(Sep-month+1.) }

define FolRuleCalc {
     case JunthruSep {
          condition month >= may .and. month <= sep
          value     FolInf - FolDiv + S_Folsm(-1) - FolFlowTarg*cfs_taf }
     case Other {
          condition always
          value     S_FolsmLevel3}}
define FolRuleCalcdv {alias FolRuleCalc kind 'RULE' units 'TAF'}

define S_FolsmLevel3adj {value min(max(FolRuleCalc,S_FolsmLevel2),S_FolsmLevel4)}
define S_FolsmLevel3dv {alias S_FolsmLevel3adj kind 'STORAGE-LEVEL' units 'TAF'}

goal S_FolsmZone3 {S_Folsm_3 < max(S_FolsmLevel3adj - S_FolsmLevel2,0.)}
goal S_FolsmZone4 {S_Folsm_4 < max(S_FolsmLevel4 - S_FolsmLevel3adj,0.)}
goal S_FolsmZone5 {S_Folsm_5 < max(S_FolsmLevel5 - S_FolsmLevel4,0.)}
goal S_FolsmZone6 {S_Folsm_6 < max(S_FolsmLevel6 - S_FolsmLevel5,0.)}

