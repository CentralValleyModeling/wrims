! D204_Adjustment.wresl
!
! Transfers on the Yuba Basin is modeled as an additional supply inputed at I204_WTS and I204_EWA
! D204_REOP represent the impacts of recharging this GW and SW substitution supply - the magnitude of
! this impact is debatable and further review should be performed before this code is adopted for Yuba River analyses
!
define Yubadv       {std kind 'WTS-Acquisition' units 'taf'}
define AnnYuba_EWAdv {std kind 'EWA-Acquisition' units 'TAF'}
define PrevYrYubadv {std kind 'Acquisition' units 'taf'}
!
define PrevYrYuba {
      case FirstYr {
            condition wateryear == 1922
            value 0. }
      case Oct {
            condition month == OCT
            value   Yubadv(prevSep) + AnnYuba_EWAdv(prevSep) }      !Adjusting for both WTS and EWA
      case otherwise {
            condition always
            value PrevYrYubadv(-1) }
}
goal setPrevYrYubadv {PrevYrYubadv = PrevYrYuba }

define D204_Adjustment {
      case first {
            condition wateryear == 1922
            value 0. }
      case Jan {
            condition month == Jan
            value PrevYrYuba*(I230*cfs_taf/(I230*cfs_taf + I230(1)*cfs_taf(1) + I230(2)*cfs_taf(2))) }
      case Feb {
            condition month == Feb
            value PrevYrYuba*(I230*cfs_taf/(I230(-1)*cfs_taf(-1)+I230*cfs_taf + I230(1)*cfs_taf(1))) }
      case Mar {
            condition month == Mar
            value PrevYrYuba*(I230*cfs_taf/(I230(-1)*cfs_taf(-1) + I230(-2)*cfs_taf(-2) + I230*cfs_taf)) }
      case Apr {
            condition month == Apr .and. PrevYrYuba>(I230(-1)*cfs_taf(-1) + I230(-2)*cfs_taf(-2)+I230(-3)*I230*cfs_taf(-3))
            value max(PrevYrYuba-D204_REOP(-1)*cfs_taf(-1)-D204_REOP(-2)*cfs_taf(-2)-D204_REOP(-3)*cfs_taf(-3), 0.)}
      case otherwise {
            condition always
            value 0. }
      }
goal set_D204_REOP {D204_REOP < D204_Adjustment*taf_cfs} ! weight is 5600
