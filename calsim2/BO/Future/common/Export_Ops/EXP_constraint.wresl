! EXP_constraint.wresl
!
! **********************************************************************************************
!
! Determine health and safety level at OMR that must be allowed
define HandS_minpump {
     case JanMay {
          condition month>=JAN .and. month<=MAY
          value 1500.}
     case Jun {
          condition month==JUN
!          value 2000.}
          value 1500.}
     case other {
          condition always
          value 0.}
}

! Take the most restrictive of the NMFS export restrictions but consider the Health
! and safety level export that must be allowed. Minimum H&S export level is removed 
! from calculation if IF is active.  This is so that if the IF is active, Through Delta 
! exports don't have to meet entire H&S pumping amount, IF can contribute.  Goal
! set_HandSminpump further down will pull in water from the IF for this.
define MAXEXP_RPA {value max( min(NMFS_MaxExp, NMFS_MaxExp_DCC, FWS_MaxExp_X2), (1-max(JonesIFSwitch,BanksIFSwitch)) * HandS_minpump)}
!
! **********************************************************************************************
!
define Export_dec_RPA {value max(0.,D409[UPSTREAM] - MAXEXP_RPA)}
define SWP_exp_dec_RPA {value max(0.,min(Export_dec_RPA,D419_swp_TD[UPSTREAM] - D418_TD[UPSTREAM]))}
define CVP_exp_dec_RPA {value max(0.,min(Export_dec_RPA,D418_TD[UPSTREAM] - D419_swp_TD[UPSTREAM]))}
define Comb_exp_dec_RPA {value max(0., Export_dec_RPA - SWP_exp_dec_RPA - CVP_exp_dec_RPA)}

define Export_dec_RPA_ {alias Export_dec_RPA kind 'temp' units 'cfs'}
define SWP_exp_dec_RPA_ {alias SWP_exp_dec_RPA kind 'temp' units 'cfs'}
define CVP_exp_dec_RPA_ {alias CVP_exp_dec_RPA kind 'temp' units 'cfs'}
define Comb_exp_dec_RPA_ {alias Comb_exp_dec_RPA kind 'temp' units 'cfs'}

!define MAXEXP_RPA_CVP {value max(tracyminpump,min(0.5*MAXEXP_RPA,D418[UPSTREAM]/*maxTracycalc*/)) }
!define MAXEXP_RPA_SWP {value max(banksminpump,MAXEXP_RPA - MAXEXP_RPA_CVP) }
define MAXEXP_RPA_CVP {value max(tracyminpump,D418_TD[UPSTREAM] - CVP_exp_dec_RPA - 0.5*Comb_exp_dec_RPA)}
define MAXEXP_RPA_SWP {value max(banksminpump,D419_swp_TD[UPSTREAM] - SWP_exp_dec_RPA - 0.5*Comb_exp_dec_RPA)}


goal set_MAXEXP_RPA_SWP {
    lhs     D419_swp_TD
    rhs     MAXEXP_RPA_SWP
    lhs<rhs penalty    0}
define MAXEXP_RPA_SWPdv {alias MAXEXP_RPA_SWP kind 'EXPORT-LIMIT' units 'CFS'}

goal set_MAXEXP_RPA_CVP {
    lhs     D418_TD
    rhs     MAXEXP_RPA_CVP
    lhs<rhs penalty    0}
define MAXEXP_RPA_CVPdv {alias MAXEXP_RPA_CVP kind 'EXPORT-LIMIT' units 'CFS'}

define MAXEXP_RPAdv {alias MAXEXP_RPA kind 'EXPORT-LIMIT' units 'CFS'}

goal set_HandSminpump {
    lhs     D418_TD + D419_TD + D418_IF + D419_IF
    case IF_on {
     condition  max(JonesIFSwitch,BanksIFSwitch) > 0.5
     rhs     HandS_minpump
     lhs>rhs penalty 0
     lhs<rhs penalty 90000}
}

!Limit Partner use of LV by export constraints.
goal constrain_D408_P {D418_TD + D419_TD + D408_P < maxexp_rpa}
