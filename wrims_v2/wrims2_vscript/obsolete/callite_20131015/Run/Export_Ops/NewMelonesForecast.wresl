/*************************************
NewMelonesForecast.wresl
 
Hao Xie   DWR (09/03/2010)

This code calculates two separate New Melones Forecasts
**************************************/

/*
The New Melones forecasts are used to determine demands and minimum flows on the Stanislaus River.
The first forecast (NMforecast1) is the February end of month storage + March thru September
inflows.  This forecast is updated each March.  NMforecast1 is used to determine the minimum fish
and wildlife flows, the Stockton East demands, and the Central San Joaquin demands.  The second
forecast (NMforecast2) is the sum of October thru September inflows.  This forecast is updated
each October.  NMforecast2 is used to determine the Oakdale Irrigation District and the Southern
San Joaquin Irrigation District.
*/

define I10 {timeseries kind 'FLOW-INFLOW' units 'TAF' convert 'CFS'}  !New Melones
define S10 {timeseries kind 'STORAGE' units 'TAF'} !New Melones Reservoir

! Define New Melones inflows ( sum of March through September inflows)
define sumI10_part {
    case start{
        condition   month<=FEB .and. wateryear==beginWY
    value 1000.}
    case marthrusep {
        condition   month >= mar .and. month <= sep
        sum(i=mar-month,mar-month+6,1) I10(i) * cfs_TAF(i) }
    case otherwise {
        condition   always
        sum(i=-month-mar,-month,1) I10(i) * cfs_TAF(i) }
        }
! New Melones forecast 1, is the February end of month storage plus March through September inflows
define NMforecast1 {
     case FebruaryBeginWY {
          condition wateryear==beginWY .and. month <= feb
          value        1700. + /*  S10(-12) + */ sumI10_part }
     case February {
          condition month == feb
          value          S10(-12) + sumI10_part }
    case marchtofeb {
        condition   always
        value       S10(prevfeb) + sumI10_part }
        }

define sumI10p_a     {alias sumI10_part units 'taf'}
define NMforecast1dv {alias NMforecast1 kind 'forecast' units 'taf'}

