/*************************************
exportratio_FAM.wresl
 
Hao Xie and Nazrul Islam DWR (02/7/2012)

Export-Inflow Ratio restriction on exports for FAM

**************************************/

! Delta Export defined as in DWRSIM Algorithm Description for Export Ratio
define(FAMmonths) ExportActual {alias  D_Jones($m)+D_Banks($m) KIND 'EXPORT-PRJ' units 'CFS'  }

! Delta Inflow defined as in DWRSIM Algorithm Description for Export Ratio
define(FAMmonths) Inflow {alias C_Hood_ANN($m)+D_Hood_NP($m)-I_HOOD($m)+AD_Mokelumne($m)+C_YoloBP($m)+AD_SJR($m)+AD_Calaveras($m)+I_MarshCr($m) kind 'INFLOW-DELTA' UNITS 'CFS'   }

! EI allowable export variable - MAXIMUM ALLOWABLE EXPORT due to EXPORT RATIO
define(FAMmonths) EiExpCtrl {std kind 'EXPORT-CTRL-EI' units 'CFS'}

! EI Ratio dependent on month
define(FAMmonths) ExpRatio {     !EI_DLTSW switch added 10/04/2010 by Z.Q. Richard Chen, DWR
    case NOREGULATION {
      condition EI_DLTSW == 0
      value 1}
    case USERDEF {  
      condition EI_DLTSW == 2
        select ratio
        from gui_EiRatio 
        where month=mv($m) }
    case feb    {       !.and. EI_DLTSW== 1
        condition mv($m) == FEB 
          select  ratio
          from    febeiratio
          where   wateryear=wy($m)}
    case rest   {       !.and. EI_DLTSW== 1
        condition always
          select    ratio
          from      eiratio
          where     month=mv($m)}
}

! Write out EI ratio
define(FAMmonths) ExpRatio_ {alias ExpRatio($m) KIND 'EI-RATIO-STD' UNITS 'NONE'}

! Compute exports allowable by the EI ratio
goal(FAMmonths) find_max_export {EiExpCtrl($m) = ExpRatio($m)*Inflow($m)}

! Restrict exports to be less than that allowable by EI ratio
goal(FAMmonths) export_comply { ExportActual($m) < EiExpCtrl($m) }

!Export-San Joaquin River Inflow Ratio 
define(FAMmonths)  esjrir_o    { 
    case EISJR_UDEF 
      {condition EISJR_DLTSW == 2 
       select offset from gui_eisjr where wyT_Sac=wyT_Sac($m),month=mv($m) }
    case NO_ESJRIR
      {condition always
       value 999999 }
    }
    
define(FAMmonths)  esjrir_m    { 
    case EISJR_UDEF 
      {condition EISJR_DLTSW == 2
       select multiplier from gui_eisjr where wyT_Sac=wyT_Sac($m),month=month($m) }
    case NO_ESJRIR
      {condition always
       value 0.0 }
    }

goal(FAMmonths) export_SJRIR_comply { lhs ExportActual($m) 
        case EISJR_UDEF{
             	condition EISJR_DLTSW == 2
             	rhs esjrir_o($m) + esjrir_m($m)*AD_SJR($m) 
             	lhs<rhs penalty 0
             	lhs>rhs penalty 999999 
             }
        }
             
             

