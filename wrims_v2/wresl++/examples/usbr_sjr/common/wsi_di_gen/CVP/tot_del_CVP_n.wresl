! This file calculates the sum of the actual CVP NOD deliveries for each type (PA, PU, PS
! and RF) for the previous project delivery season.  For PS arcs, only the delivery values for
! April - October are totaled since March and November - February values are non-project water right diversions.

! This file is used to calculate the Actual DI to be used in the curve generation procedure.


! ================================ AG Deliveries ===============================

define cvpdel_PA {  

       case      MAR  {
       condition month == MAR .and. wateryear > 1922
       sum(i=-1,-12,-1) (D_WKYTN_2_PA(i) + D_SAC294_3_PA(i) + D_CCL005_4_PA1(i) + D_TCC022_4_PA2(i) + D_TCC036_7N_PA(i) + D_TCC081_7S_PA(i) + D_TCC111_7S_PA(i) + D_FTR021_16_PA(i))* cfs_taf(i) }

       case      otherwise {
       condition always
       value     0 }
       }


! =============================== Urban Deliveries ===================================
! Include placeholders for nine CVP MI contractors with mixed diversion of CVP and Water Forum water - CVP delivery assumes zero shortage! Include placeholders for nine CVP MI contractors with mixed diversion of CVP and Water Forum water - CVP delivery assumes zero shortage

define cvpdel_PU { 

       case      MAR  {
       condition month == MAR .and. wateryear > 1922
       sum(i=-1,-12,-1) (D_WTPBUK_3_PU3(i) + D_WTPCSD_2_PU(i) + D_WKYTN_2_PU(i) + D_WTPJMS_3_PU1(i) + D_SAC294_3_PU2(i) + CL2_FOLSMdv(i) + CL2_SJWD_Sdv(i) + CL2_ELDORdv(i) + CL2_ROSEVdv(i) + CL2_CARECdv(i) + CL2_SMUDdv(i) + CL2_PCWA_Sdv(i) + CL2_SCWAdv(i) + CL2_PCWA_Adv(i))* cfs_taf(i) }

       case      otherwise {
       condition always
       value     0 }
       }


! =============================== Settlement Deliveries (previous April - October only) ========

define cvpdel_PS {

       case      MAR  {
       condition month == MAR .and. wateryear > 1922
       sum(i=-5,-11,-1) (D_SAC296_2_SA(i) + D_WTPFTH_3_SU(i) + D_WTPFTH_2_SU(i) + D_SAC289_3_SA(i) + D_SAC178_8N_SA1(i) + D_SAC159_8N_SA1(i)+ D_GCC027_8N_SA2(i) + D_SAC159_8S_SA1(i) + D_GCC046_8S_SA2(i) + D_SAC121_8S_SA3(i) + D_SAC109_8S_SA3(i) + D_SAC196_9_SA1(i) + D_SAC162_9_SA2(i) + D_SAC141_18_SA(i) + D_SAC122_19_SA(i) + D_SAC115_19_SA(i) + D_SAC099_19_SA(i) + D_SAC091_19_SA(i) + D_SAC083_21_SA(i) + D_SAC074_21_SA(i) + D_SAC082_22_SA1(i) + D_SAC078_22_SA1(i))* cfs_taf(i) }

       case      otherwise {
       condition always
       value     0 }
       }


! ================================== Refuge Deliveries =========================

define cvpdel_PR {

       case      MAR  {
       condition month == MAR .and. wateryear > 1922
       sum(i=-1,-12,-1) (D_GCC027_8N_PR1(i) + D_GCC039_8N_PR2(i) + D_GCC046_8S_PR(i) + D_CBD037_8S_PR(i) + D_WEC000_11_PR(i) + D_RVC005_11_PR(i) + D_SAC196_9_PR(i))* cfs_taf(i) }

       case      otherwise {
       condition always
       value     0 }
       }


! ============================= Actual Total Deliveries ===========================

define tot_del_cvp_n { value cvpdel_PA + cvpdel_PU + cvpdel_PS + cvpdel_PR }
