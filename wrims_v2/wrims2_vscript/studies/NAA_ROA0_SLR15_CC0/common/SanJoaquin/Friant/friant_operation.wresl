! Friant_operation.wresl
! Determination of Friant San Joaquin River minimum release
! Revised by C. Hsu, 02/25/03, USBR for review and documentation project
!##############################################################################
/*
This file defines the Madera and Friant-Kern Canal demands and deliveries
*/
! Define Fraint-Kern Canal Demands
define demand_D18A {std kind 'Friant-Demand' units 'TAF' }
! Define Madera Canal demands
define demand_D18B {std kind 'Friant-Demand' units 'TAF' }
! Friant-Kern Canal release
goal set_FK_release { demand_D18A = D18A_ini - FK_Adj + FK_add }
! Madera Canal release
goal set_Madera_release { demand_D18B = D18B_ini + Madera_add }

/*
This file defines the total deliveries from Friant which includes Madera Canal and Friant-Kern Class I & II, canal losses,
and additional delivery
*/
! Total delivery from Friant
define total_Fr_del {
    value    Madera_C1_del + Madera_C2_del + Madera_loss + Madera_add
             + FK_C1_del + FK_C2_del + FK_loss - FK_Adj + FK_add }

! Friant Kern canal Class 2 water including adjustment
define FK_final_C2 {value FK_C2_del - FK_Adj}
define FK_final_C2_ {alias fk_c2_del kind 'temp' units 'taf'}
define FK_adj_ {alias fk_adj kind 'temp' units 'taf'}


! Define total canal adjustments to river release
define Fr_tot_adj { value FK_adj - FK_add - Madera_add }
! Snowmelt release (SJR) determination, total snowmelt release minus 215 delivery of snowmelt release
define Fr_snow_rel { value max(0.0,(fr_for_spill_cor * Fr_srp / 100.) - FK_add - Madera_add) }

/*
Snowmelt release
C18A target is computed considering releases that will be made for minimum
flows and deliveries.  So C18A is above and beyond C18M.
*/

define est_RF_to_MC {value max(0.0, SIO(-1) + I18 *cfs_taf - curr_Fr_del - Friant_est_evap
			- (S18level5 + S17level3 + mam_fc_credit  - Friant_fs_req) - Madera_add - FK_add)}

define max_SM_to_MC {value max(0.,min(Fr_snow_rel, Madera_cap_head * cfs_taf - D18B_ini - Madera_add))}

!Limit Madera Surplus release to estimated snowmelt release and flood release
goal lim_D18F_surplus {D18F < est_RF_to_MC*taf_cfs + max_SM_to_MC*taf_cfs}

!Limit SWP and CVP Madera Canal flows to flood flows
goal limMadSWPCVPdiv {D18B_SWP + D18B_CVP < D18F}

!Limit C18A to snowmelt release

!goal set_C18A { C18A < Fr_snow_rel * taf_cfs}

!During flood or snowmelt spills, approximately 7% of the spill will be to Madera Canal capped at 1000 cfs with half
!going to the Fresno River and half going to Chowchilla

goal set_D18f_max {D18F < max(0., Madera_cap_head - D18B_ini * taf_cfs - Madera_add * taf_cfs) }
goal lim_D18F_by_C18AandF {D18F < 0.07*C18F}
goal lim_D18F_1000 {D18F < 1000}
goal set_D18F {D18F = D590F + C590F}
goal set_C590_16B {C590_16B = D582_16B}
define srp_C590F {std kind 'soft-const' units 'cfs'}
define slk_C590F {std kind 'soft-const' units 'cfs'}
goal share_D18F {C590F - D590F + slk_C590F - srp_C590F = 0}

!Route Madera 16B water
goal route_Mad_16B {D18B_16B = D590_16B + D582_16B}

!Subdivide Madera Canal 16B water into direct and in-lieu recharge.
define D582_16B_DR {std kind 'flow-delivery' units 'cfs'}
define D582_16B_IL {std kind 'flow-delivery' units 'cfs'}
define D582_16B_PC {std kind 'flow-delivery' units 'cfs'}
define D590_16B_DR {std kind 'flow-delivery' units 'cfs'}
define D590_16B_IL {std kind 'flow-delivery' units 'cfs'}
define D590_16B_PC {std kind 'flow-delivery' units 'cfs'}

goal split_D582_16B {D582_16B = D582_16B_DR + D582_16B_IL + D582_16B_PC}
goal split_D590_16B {D590_16B = D590_16B_DR + D590_16B_IL + D590_16B_PC}

goal lim_D590_16B_DR {D590_16B_DR < 200}
goal lim_D590_16B_IL {D590_16B_IL < 0}
goal lim_D582_16B_DR {D582_16B_DR < 175}

define Mad_ag_pat_pct {
    select percent
    from Madera_delivery_total
    given delivery = 1500.
    use minimum
    where contract_month = contract_month }

goal lim_D582_16B_IL {D582_16B_IL < 25.*Mad_ag_pat_pct/24.}

define lim_Mad_16B_PC {
	case	Demand {
		condition	range(month,dec,jan) .and. I53 < 30*taf_cfs
		value		1122.
	}
	case	NoDemand {
		condition	always
		value		0.
	}
}

goal set_lim_D582_16B_PC {D582_16B_PC < 0.85*lim_Mad_16B_PC}
goal set_lim_D590_16B_PC {D590_16B_PC < 0.15*lim_Mad_16B_PC}

define tot_mad_16B_ {std kind 'annual-delivery' units 'taf'}

define tot_mad_16B {
	case October {
		condition	month == oct
		value		0.0
	}
	case otherwise {
		condition	always
		value		(D590_16B(-1) + D582_16B(-1) - D590_16B_PC(-1) - D582_16B_PC(-1))*cfs_taf(-1) + tot_mad_16B_(-1)
	}
}

goal ann_mad_16B_lim {D582_16B + D590_16B - D582_16B_PC - D590_16B_PC < max(0., 35. - tot_mad_16B)*taf_cfs}

goal save_tot_mad_16B {tot_mad_16B_ = tot_mad_16B}



! Set limits on Friant-Kern Canal releases
!goal set_FK_rel { D18A_FWUA < demand_d18A * taf_cfs}
! Friant Kern Canal breakdown
goal set_D18A_C1 {D18A_C1 < FK_C1_del*taf_cfs}
goal set_D18A_main { D18A_C2 < max(0., FK_final_C2*taf_cfs)}
! Set limits on Friant Kern Canal 215 water
goal set_D18A_215 { D18A_215 < FK_add * taf_cfs }

! Set limits on Madera Canal releases
goal set_M_rel { D18B < demand_d18B * taf_cfs + D18F + tot_und_D18B_C1_*taf_cfs*Mad_C1_frc + tot_und_D18B_C2_*taf_cfs*Mad_tot_frc + D18B_16B }
goal set_max_MC { D18B < Madera_cap_head}
! Madera Canal breakdown
goal set_D18B_C1 {D18B_C1 < Madera_C1_del*taf_cfs + tot_und_D18B_C1_*taf_cfs*Mad_C1_frc }
goal set_D18B_main { D18B_C1 + D18B_C2 < max(0., D18B_ini * taf_cfs - Madera_loss*taf_cfs) + tot_und_D18B_C1_*taf_cfs*Mad_C1_frc + tot_und_D18B_C2_*taf_cfs*Mad_tot_frc }
! Set limits on Madera Canal 215 water
goal set_D18B_215 { D18B_215 < Madera_add * taf_cfs }

goal set_und_D18B_C1 {und_D18B_C1 - re_und_D18B_C1 = Madera_C1_del - D18B_C1*cfs_taf}
goal set_und_D18b_c2 {und_D18B_C2 - re_und_D18B_C2 = Madera_C2_del - D18B_C2*cfs_taf}
define und_D18A_C1 {alias FK_C1_del - D18A_C1*cfs_taf kind 'under-delivery' units 'taf'}
define und_D18A_C2 {alias FK_C2_del - D18A_C2*cfs_taf kind 'under-delivery' units 'taf'}

goal restrict_D16B_A {D18A_16B_A + D18B_16B_A + D18A_215 + D18B_215 < Fr_fc_rel*taf_cfs}

