! stan_FW_pulse.wresl
! Edward Chang
! 5/01/2000
! Joint Model
! This code calculates the Stanislaus operations for fish and wildlife pulse flows
!########################################################################################
/*
The minimum fish and wildlife flows are determined using the New Melones forecast, NMforeast1.
This forecast determines an annual delivery requirement from a look-up table.  From the annual
delivery requirement another look-up table is used to determine the monthly requirements.
In April and May, a pulse flow requirement is also calculated, but the pulse flow only applies
during the last 16 days in April and the first 15 days in May.  The goal of the resulting monthly
requirement is to maintain a minimum fish and wildlife flow downstream of Goodwin Dam.  To ensure
a minimum flow, it is only necessary to impose this requirement on reaches C_STANGDWN and C_STANRIPN
*/
/*
Fish & Wildlife flows
*/
! Define Cycle 3 minimum Stanislaus F & W pulse flows Apr 15 - May 15
!In April uses pre-RPA std (in order to set VAMP baseline), in May uses RPA std
define stanfish_yr {
    case STAN_RPAOFF{
        condition STAN_RPA == 0 .and. range(month, apr, may)
        select stanf from stan_yr_90 given NMF = NMforecast1 use linear }! every mon if RPA of
    case April {
        condition month==APR .and. STAN_RPA == 1
        select stanf from stan_yr_90 given NMF = NMforecast1 use linear }
    case notApr {
        condition always 
        select stanf from stan_yr given NMF = NMforecast1 use linear}
}
! Alias stanfish_yr for output
define stanfish_yrDV {alias stanfish_yr units 'taf'}


define stanpulse {
    case STAN_RPAOFF{
        condition STAN_RPA == 0 .and. range(month, apr, may)
        select          pulse from stan_pulse given stanyr = stanfish_yr use linear }
    case April {
        condition       month == apr .and. STAN_RPA == 1
        select          pulse from stan_pulse given stanyr = stanfish_yr use linear }
    case May {
        condition       month == may .and. STAN_RPA == 1
        select          pulse from stan_pulse_rpa given stanyr = stanfish_yr use linear }
    case otherwise {
        condition       always
        value           0. }
        }
define stanpulse_dv {alias stanpulse kind 'fish-pulse-req' units 'cfs'}

! Define revised total annual fish releases
! Base MIF + Enhanced flow based on estimated delivery, estimated inflow, and desired target storage
! Based on Modifications by Hilts 20090205 to eliminate "Transitional Plan" related logic.  Setting STANFISH_YR2=STANFISH_YR1

define stanfish_yr2 { select stanf from stan_yr given NMF = NMforecast1 use linear }

/* define stanfish_yr2 {
        case firstyr {
                condition wateryear==beginWY .and. month <= mar
                value 98.9 + stan_dem_est + min(204.,max(NMforecast3-((310.-stan_dem_est)-stan_dem_est)-1700.,0.0))
        }
        case march {
                condition month == mar
                value 98.9 + stan_dem_est + min(204.,max(NMforecast3-((310.-stan_dem_est)-stan_dem_est)-1700.,0.0))
        }
        case otherwise {
                condition always
                value 98.9 + stan_dem_estdv(prevMar) + min(204.,max(NMforecast3dv(prevMar)-((310.-stan_dem_estdv(prevMar))-stan_dem_estdv(prevMar))-1700.,0.0))
        }
} */

define stanpulse2 {
    case OnlyInAprMay {
        condition month == apr .or. month == may
        select pulse from stan_pulse given stanyr = stanfish_yr2 use linear}
    case otherwise {
        condition always
        value 0. }
}

! Stanislaus River Temperature Management flow surrogates for June - Sep
!define stantemp {
!       case June_Sep {
!                condition month >= jun .and. month <= sep
!                select tflow from stan_temperature given stanyr = stanfish_yr2 use minimum where month = month}
!       case otherwise {
!            condition always
!            value 0.0}
!}

! Stanislaus F & W pulse flows for Cycle 3 April-May release, CALSIM uses internal units of cfs
goal setC_STANGDWNcycle3 {
    lhs C_STANGDWN_mif
    case AprMay {
        condition   month >= apr .and. month <= may
!        rhs         max(stanpulse2, stantemp)
        rhs         stanpulse
        lhs<rhs     penalty 0 }
    case otherwise {
        condition   always
        rhs         C_STANGDWN_mif[SJR_WQ1]
          lhs<rhs        penalty 0 }
        }
! The "INSTREAM" flow requirements are only active in Oct-Dec, so for the pulse period
! of April and May when this code is used the element is simply zeroed out for convenience
goal setC_STANGDWNINSTREAM1 {C_STANGDWNINSTREAM = 0.}
! Stanislaus River minimum F & W pulse flows for Cycle 3 during April- May release, CALSIM uses internal units of cfs
goal setC_STANRIPNcycle3 {
    lhs C_STANRIPN_mif
    case AprMay {
        condition   month >= apr .and. month <= may
!        rhs         max(stanpulse2, stantemp)
        rhs         stanpulse
        lhs<rhs     penalty 0 }
    case otherwise {
        condition   always
        rhs         C_STANRIPN_mif[SJR_WQ1]
        lhs<rhs     penalty 0 }
        }
