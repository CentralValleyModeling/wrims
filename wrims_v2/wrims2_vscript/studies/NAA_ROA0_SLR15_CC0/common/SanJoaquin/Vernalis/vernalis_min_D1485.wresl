! vernalis_min.wresl

! Originally coded by Edward Chang (DWR) 06/27/2000 for Vernalis Minimum Flows

/*
 In Cycle 6:
 The following code defines and meets the 1995 Water Quality Control Plan codified in D1641
 (WQCP) Vernalis February thru June minimum flows (excluding the pulse flow period Apr15-May15).

 The Vernalis minimum flow is a function of:
     1)  60-20-20 index (San Joaquin basin water year type)
     2)  Where is 2ppt isohaline (measured as 2.64 mmhos/cm surface salinity) is required to be
 If the day weighted average requirement at Roe, Chipps and Confluence is less than
 or equal to 74 km, X2 is required to be at or West of Chipps, i.e. <= 74 km

                    X2 Required to be        X2 Required to be at or
                    East of Chipps           West of Chips
 Water year type    Minimum flow Req (cfs)   Minimum flow Req (cfs)
 ----------------------------------------------------------------
   Wet              2130                     3420
    AN              2130                     3420
    BN              1420                     2280
   Dry              1420                     2280
   Crt               710                     1140

 Release for the requirement is made from New Melones and may be restricted
 by a inputted limit on allocation of Stan water for this purpose.  See STAN_YR.TABLE
 The USBR 1997 two-year Interim Plan of Operations used the following allocation table:
 Water Supply   Annual
  Forecast     Allocation
   (TAF)         (TAF)
 -----------------------
     0.        0.
     1400.     0.
     2000.     0.
     2500.     1000.
     3000.     1000.
     6000.     1000.

where the Water Supply Forecast was comprised of End-of-Feb New Melones storage in taf + forecasted inflow
to New Melones from Mar1 to Sep30 in taf.  Usually in modeling thus far, this "forecast" was with perfect foresight.

Hilts May 5, 2009: Per NOAA's latest draft INTERIM RPA for Vernalis get requirement based on USBR water supply.
*/

define NOAAVernMin_req {
     case AprORMay {
          condition month == apr .or. month == may
          select NOAAmin from NOAAVern60dayMin given WSP = NMforecast1 use minimum }
     case otherwise {
          condition always
          value     0. }
          }

define X2EastOfChipps {select VernMinReqEast from VernMin where wyt_SJR=wyt_SJR }
define X2WestOfChipps {select VernMinReqWest from VernMin where wyt_SJR=wyt_SJR }


define VernMin_req {
/*
     case considerNOAA {
          condition month == apr .or. month == may
          value ((max (NoaaVernMin_req,X2WestOfChipps)*daysX2_chs)+
                 (max (NoaaVernMin_req,X2EastOfChipps)*(daysin - daysX2_chs)))/daysin}
*/
/*
     case considerX2 {
          condition month >= feb .and. month <= jun
          value ((daysX2_chs*X2WestOfChipps)+((daysin - daysX2_chs)*X2EastOfChipps))/daysin}
*/
     case otherwise {                                                                                                        !WB
          condition always                                                                                                   !WB
          value     0. }                                                                                                     !WB
          }                                                                                                                  !WB
define VernMin_reqdv {alias VernMin_req kind 'flow-requirement' units 'cfs'}

/*
Vernalis minimum flow requirement deficiency. The deficiency is the
difference between the Vernalis minimum requirement less the San Joaquin River
inflow to the Delta in Cycle 2 (SJR_WQ1) and what is released from New Melones
for the DO in cycle 5 (VAMP_AND_DO).
*/
define VernMin_def { value max (0., VernMin_req - C639[SJR_WQ1] - C10DO[VAMP_AND_DO])}

define VernMin_def_mon {
        case April {
                condition       month == apr
                value   VernMin_def*14./30.
        }
        case May {
                condition       month == may
                value   VernMin_def*16./31.
        }
        case otherwise {
                condition       always
                value   VernMin_def
        }
}
define VernMin_defmonDV {alias VernMin_def_mon kind 'flow-requirement' units 'cfs'}

/*
 Vernalis minimum flow requirement capapcity limits the amount of water to release
 based on the New Melones Forecast.
*/
define VernMinCap { select VernMinCap from stan_yr given NMF=NMforecast1 use minimum }

! Vernalis minimum release cumulative total March through June (summed from Feb to current month-1).
define VernMinRelTotprv {
     case MarchthruJun {
          condition month >= mar .and. month <= jun
          sum(i=-1,prevfeb,-1) C10min(i)*cfs_taf(i) }
     case otherwise {
          condition always
          value     0. }
          }
/*
 Vernalis minimum release capacity remaining (Capacity less monthly cumulative total)
 February through June and reset each February.
*/
define VernMinCapRem {
     case February {
          condition month == feb
          value     VernMinCap*TAF_cfs }
     case MarchthruJun {
          condition month >= mar .and. month <= jun
          value     max (0.,VernMinCap - VernMinRelTotprv)*TAF_cfs }
     case otherwise {
          condition always
          value     0. }
          }
/*
 Vernalis minimum release remaining from Goodwin maximum capacity.  Pulse flow periods
 are addressed in April and May.  Flood flow does not count towards contributing
 water to the Vernalis minimum flow requirement.
*/
define VernMinRemGoodRipon {
/*     case April {
          condition month == apr
          value     max(0.,maxGoodwin - C520[SJR_WQ1] + C520F[SJR_WQ1])*14./30.}
     case MayOnly {
          condition month == may
          value     max(0.,maxGoodwin - C520[SJR_WQ1] + C520F[SJR_WQ1])*16./31.} */
     case JunethruSep {
          condition month >= jun .and. month <= sep
          value max(0.,maxGoodwin - C520[VAMP_AND_DO] + C520F[VAMP_AND_DO]) }
     case otherwise {
          condition always
          value  max(0., maxGoodwin - C520[SJR_WQ1] + C520F[SJR_WQ1]) }
          }

/*
 Vernalis minimum flow requirement to be met is based on the least value of
 the maximum allowed release from Goodwin less what has been released for this month,
 or the Vernalis monthly flow requirement less flow in the Delta in cycle 1 (SJR_WQ1)
 less releases from New Melones in the DO cycle (VAMP_AND_DO) or the Vernalis
 minimum release capacity (limit) less the cumulative total released to meet the requirement.
*/
define VernMin_reqtobemet { value max(0.,min (VernMinRemGoodRipon, VernMin_def_mon,VernMinCapRem))}
define AL_VrnMintobemet {alias VernMin_reqtobemet kind 'temp' units 'cfs'}


! Set minimum flow in C10 equal to the Vernalis minimum flow requirment to be met.
goal meetVernMin {
     lhs  C10MIN
     case FebToJun {
          condition month >= feb .and. month <= jun
          rhs       VernMin_reqtobemet
          lhs<rhs   penalty 0. }
     case otherwise {
          condition always
          rhs       0. }
          }
