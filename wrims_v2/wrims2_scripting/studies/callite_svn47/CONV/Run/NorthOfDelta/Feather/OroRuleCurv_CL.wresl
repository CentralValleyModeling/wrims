/************************************
OroRuleCurv_CL.wresl

Z.Q. Richard Chen DWR 5/06/2010
                     11/30/2010 added OMRBOON comments 
Sets Oroville rule curve (Oroville Storage Level 3 and Level 4 values) 
**************************************/

/***
    "OroSepTarg" is determined by allocation, Septermber Storage, Level 5 value (b_X).
    Set June to September Target Storage as "OroNextMonTarg" as min of 
    Level 5 value and OroSepTarg plus the average available storage above "OroSepTarg"
***/
define OroSepTarg { ! DrainTar_buffer = 0 when int(OMRBOON)==1 as defined in delcar_swp.wresl
  case a_X{
    condition wateryear==1922 .and. month<=dec
    value min(1000. + 0.5*max(S_Orovl(prevSep) - 1000. - DrainTar_buffer ,0.), S_OrovlLevel5) }
  case b_X{
    condition always
    value min(1000. + 0.5 * (swp_allocation-tableA_loss)/SWP_TableA
                          * max(S_Orovl(prevSep) - 1000. - DrainTar_buffer ,0.), S_OrovlLevel5) }
}

define OroNextMonTarg {
          value min(OroSepTarg + (max(S_Orovl(-1) - OroSepTarg,0.)/(Sep-month+1.))*(Sep-month), S_OrovlLevel5)
}

/***
    Set Level 4 value to "OroNextMonTarg" for June to September and 
                      to Level 5 value for other month
***/
define S_OrovlLevel4 { ! An alternative method
     case JuntoSep {
      condition month >= jun
      value     OroNextMonTarg }
     case Other {
          condition always
          value     S_OrovlLevel5 }
}

define S_OrovlLevel4dv {alias S_OrovlLevel4 kind 'STORAGE-LEVEL' units 'TAF'}
define OroSepTarget {alias OroSepTarg kind 'STORAGE-LEVEL' units 'TAF'}
define OroPreSepStor {alias S_Orovl(prevSep) kind 'STORAGE' units 'TAF'}
define OroNextMonTarget {alias OroNextMonTarg kind 'STORAGE-LEVEL' units 'TAF'}

/***
    Set Level 3 value as "S_OrovlLevel3adj"
***/
define S_OrovlLevel3 {value min(2470.,S_OrovlLevel4) }

define OroInf {value (I_Orovl)*cfs_taf }
define OroInfEst {sum(i=0,SEP-month,1) (I_Orovl(i))*cfs_taf(i) }

define OroDiv {value dem_d6_PWR }
define OroDivEst {sum(i=0,SEP-month,1) dem_d6_PWR(i) }
define OroSupEst {value max(0.0,OroInfEst - OroDivEst + S_Orovl(-1)- S_OrovlLevel3)}
define OroSupEstdv {alias OroSupEst kind 'FORECAST' units 'TAF'}
define OroFlowTarg {value (OroSupEst/cfs_taf)/(Sep-month+1.) }

define OroRuleCalc {
     case JunthruSep {
          condition month >= may .and. month <= sep
          value     OroInf - OroDiv + S_Orovl(-1) - OroFlowTarg*cfs_taf }
     case Other {
          condition always
          value     S_OrovlLevel3}}
define OroRuleCalcdv {alias OroRuleCalc kind 'RULE' units 'TAF'}

define S_OrovlLevel3adj {value min(max(OroRuleCalc,S_OrovlLevel2),S_OrovlLevel4)}
define S_OrovlLevel3dv {alias S_OrovlLevel3adj kind 'STORAGE-LEVEL' units 'TAF'}

goal S_OrovlZone3 {S_Orovl_3 < max(S_OrovlLevel3adj - S_OrovlLevel2,0.)}
goal S_OrovlZone4 {S_Orovl_4 < max(S_OrovlLevel4 - S_OrovlLevel3adj,0.)}
goal S_OrovlZone5 {S_Orovl_5 < max(S_OrovlLevel5 - S_OrovlLevel4,0.)}
goal S_OrovlZone6 {S_Orovl_6 < max(S_OrovlLevel6 - S_OrovlLevel5,0.)}

